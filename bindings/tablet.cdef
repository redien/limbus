
# Limbus - A cross-platform multimedia library.
# Written in 2014 by Jesper Oskarsson jesosk@gmail.com
#
# To the extent possible under law, the author(s) have dedicated all copyright
# and related and neighboring rights to this software to the public domain worldwide.
# This software is distributed without any warranty.
#
# You should have received a copy of the CC0 Public Domain Dedication along with this software.
# If not, see <http://creativecommons.org/publicdomain/zero/1.0/>.

class Limbus::Tablet : Limbus::InputDevice
	var tablet : ptr

	signal on_button_event( button : int, pressed : bool )
	signal on_motion_event( x : int, y : int, pressure : int )
	signal on_proximity_event( in : bool )

	property context_width : int
	get
		return limbus::lb_tablet_get_context_width( this.tablet )
	set
		limbus::lb_tablet_set_context_width( this.tablet, value )
	end

	property context_height : int
	get
		return limbus::lb_tablet_get_context_height( this.tablet )
	set
		limbus::lb_tablet_set_context_height( this.tablet, value )
	end

	def construct( dispatcher : Limbus::InputDispatcher ) : void
		this.tablet = limbus::lb_tablet_construct()
		dispatcher.add_input_device( this )
	end

	def destruct() : void
		limbus::lb_tablet_destruct( this.tablet )
	end
	
	def get_impl() : ptr
		return this.tablet
	end

	def poll_events() : bool
		var found_event : bool
		found_event = false

		while limbus::lb_tablet_next_event( this.tablet ) == 1 do
			var e : int
			e = limbus::lb_tablet_get_event_type( this.tablet )

			if e == limbus::LBTabletEvent::LBTabletEventButtonPress then
				on_button_event( limbus::lb_tablet_get_event_button( this.tablet ),
								 true )
			end

			if e == limbus::LBTabletEvent::LBTabletEventButtonRelease then
				on_button_event( limbus::lb_tablet_get_event_button( this.tablet ),
								 false )
			end

			if e == limbus::LBTabletEvent::LBTabletEventProximityIn then
				on_proximity_event( true )
			end

			if e == limbus::LBTabletEvent::LBTabletEventProximityOut then
				on_proximity_event( false )
			end

			if e == limbus::LBTabletEvent::LBTabletEventMotion then
				on_motion_event( limbus::lb_tablet_get_event_x( this.tablet ),
								 limbus::lb_tablet_get_event_y( this.tablet ),
								 limbus::lb_tablet_get_event_pressure( this.tablet ) )
			end

			found_event = true
		end
		
		return found_event
	end
end

