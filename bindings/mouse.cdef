
# Limbus - A cross-platform multimedia library.
# Written in 2014 by Jesper Oskarsson jesosk@gmail.com
#
# To the extent possible under law, the author(s) have dedicated all copyright
# and related and neighboring rights to this software to the public domain worldwide.
# This software is distributed without any warranty.
#
# You should have received a copy of the CC0 Public Domain Dedication along with this software.
# If not, see <http://creativecommons.org/publicdomain/zero/1.0/>.

class Limbus::Mouse : Limbus::InputDevice
	var mouse : ptr

	signal on_button_event( button : int, pressed : bool, x : int, y : int )
	signal on_motion_event( x : int, y : int )

	property x : int
	get
		return limbus::lb_mouse_get_x( this.mouse )
	set
		limbus::lb_mouse_set_x( this.mouse, value )
	end
	property y : int
	get
		return limbus::lb_mouse_get_y( this.mouse )
	set
		limbus::lb_mouse_set_y( this.mouse, value )
	end
	
	property visible : bool
	get
		var result : int
		result = limbus::lb_mouse_get_visibility( this.mouse )
		if result == 0 then
			return false
		end
		return true
	set
		if value == true then
			limbus::lb_mouse_set_visibility( this.mouse, 1 )
		end
		if value == false then
			limbus::lb_mouse_set_visibility( this.mouse, 0 )
		end
	end


	def construct() : void
		this.mouse = limbus::lb_mouse_construct()
	end

	def destruct() : void
		limbus::lb_mouse_destruct( this.mouse )
	end
	
	def get_impl() : ptr
		return this.mouse
	end

	def poll_events() : bool
	    var found_event : bool
		found_event = false
		while limbus::lb_mouse_next_event( this.mouse ) == 1 do
			var e : int
			e = limbus::lb_mouse_get_event_type( this.mouse )
			
			if e == limbus::LBMouseEvent::LBMouseEventButtonPress then
				on_button_event( limbus::lb_mouse_get_event_button( this.mouse ),
								 true,
								 limbus::lb_mouse_get_event_x( this.mouse ),
								 limbus::lb_mouse_get_event_y( this.mouse ) )
			end

			if e == limbus::LBMouseEvent::LBMouseEventButtonRelease then
				on_button_event( limbus::lb_mouse_get_event_button( this.mouse ),
								 false,
								 limbus::lb_mouse_get_event_x( this.mouse ),
								 limbus::lb_mouse_get_event_y( this.mouse ) )
			end

			if e == limbus::LBMouseEvent::LBMouseEventMotion then
				on_motion_event( limbus::lb_mouse_get_event_x( this.mouse ),
								 limbus::lb_mouse_get_event_y( this.mouse ) )
			end

			found_event = true
		end
		
		return found_event
	end
end

